grammar edu.cmu.sei.eebm.EEBM with org.eclipse.xtext.xbase.Xbase

generate eEBM "http://www.cmu.edu/sei/eebm/EEBM"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

Roadmap: 
	(name = 'Roadmap' STRING)?
	components += (IntentionalElement)+
	people += (Stakeholder)+
	trajectories +=(Decision|Path)+
;

IntentionalElement: // superclass. Implies Goals and Options are semantically equivalent ...
	Goal | Option
;

Path:  // should be "roadmap"?
	name = ID
	path+=Option+
;

// Options, Decisions, 
Option returns Option: 'Option' name=QualifiedName (':' title=STRING)? 
    '[' ( 
    	(description=Description)?
    	&('refines' refinesReference+=[IntentionalElement]+)?
    	&('conflicts' conflictsReference+=[IntentionalElement]+)? 
      	&('priority' &priority=PriorityEnum)?
      	&('time' time=INT)? //TODO: Validation should implement what an integer time refelects, e.g.seconds, weeks
      	&('cost' cost=INT)?
      	&('completion' date=STRING)? //TODO: validate in custom validator
      	&('delay cost' cod=INT)? 
    )
    ']'
;

enum PriorityEnum:
	HIGH | MED | LOW
;
// People
Stakeholder: 
	Consultant | Participant
;

Participant:
	(
		('Participant' name=ID)
		& (preferences += Preference)?
	)
;

Consultant:
	'Consultant' name=ID
;

Decision: 'Decision' name=ID
	'[' (
	('rationale' rationale=STRING)
	&('responsible' responsible=[Stakeholder])?
	&('response measure' response_measure = STRING)? //TODO could take this from ALISA Verify
	&('due' due=STRING)?
	&('options:' (
		 '{' options+=[Option] (',' options+=[Option])* '}')?
		 )
	) ']'
;

//Goals and requirements

Preference: 
	'prefers(' first=[Option] ',' last=[Option] ')' //TODO: verify these exist and are not equal
;

//NE: we could just import this from ALISA
Goal returns Goal:
	'Goal' name=QualifiedName (':'  title=STRING )? ("refines" superType=[Goal])? // short string for title
	'['  
	 	(description=Description)? 
	 	&(rationale=Rationale)? 
		//	 &(changeUncertainty=Uncertainty)? 
	 	&('see' 'document'  docReference+=STRING)?  //the document e.g. business strategy
	
	 ']';
	 
// This comes from ALISA/AADL, and would probably be better taken from there, 
// e.g. the AADL properties and unit types. Xbase may have these too. 
// @TODO determine what unit types are necessary and where to define them.
Description : 
	'description' description += DescriptionElement+
;

DescriptionElement :
 	text = STRING //| showValue = ShowValue |thisTarget?='this'| image=ImageReference
 ;

Rationale: 'rationale' text=STRING ;
